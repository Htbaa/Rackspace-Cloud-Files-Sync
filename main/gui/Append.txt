'Start of external Append File
'Your additional code should be here!

Local window:TWindow = New TWindow
window.CreateForm()
window.Init()
While TForm.FormsExist()
	WaitSystem
Wend


Type TProgressBarUpdater
	Field currentFileSize:Long
	Field currentFileTotalSize:Long
	Field totalSize:Long
	Field dataProcessed:Long
	Field totalFileCount:Long
	Field fileCount:Long

	Field ProgBarTotal:TGadget
	Field ProgBarCurrent:TGadget
	Field LabelCurrentFile:TGadget

	Method SetGadgets(ProgBarTotal:TGadget, ProgBarCurrent:TGadget, LabelCurrentFile:TGadget)
		Self.ProgBarTotal = ProgBarTotal
		Self.ProgBarCurrent = ProgBarCurrent
		Self.LabelCurrentFile = LabelCurrentFile
	End Method

	Method InitProgressBars()
		UpdateProgBar(Self.ProgBarTotal, 0.0)
		UpdateProgBar(Self.ProgBarCurrent, 0.0)
		SetGadgetText(Self.LabelCurrentFile, "")
	End Method

	Method SendMessage:Object(message:Object, context:Object)
		DebugLog String(message)
		Select String(message)
			Case "DirectoryIndex"
				Self.InitProgressBars()

				Self.totalSize = TDirectoryIndex(context).TotalSize()
				Self.fileCount = 0
				Self.totalFileCount = TDirectoryIndex(context).fileList.Count()

			Case "Container"
				Local container:TRackspaceCloudFilesContainer = TRackspaceCloudFilesContainer(context)
				Self.InitProgressBars()
				Self.totalSize = container.BytesUsed()
				Self.fileCount = 0
				Self.totalFileCount = container.ObjectCount()

			Case "Processing"
				UpdateProgBar(Self.ProgBarCurrent, 0.0)
				SetGadgetText(Self.LabelCurrentFile, TFile(context).FullName())

			Case "Processed"
				Local cloudFile:TRackspaceCloudFileObject = TRackspaceCloudFileObject(context)
				Self.dataProcessed :+ cloudFile.Size()
				Self.fileCount :+ 1

			Case "Skipping"
				Local file:TFile= TFile(context)
				SetGadgetText(Self.LabelCurrentFile, file.FullName())

				Self.currentFileSize = file.size
				Self.currentFileTotalSize = file.size
				
				Self.dataProcessed :+ file.size
				Self.fileCount :+ 1

			Case "Finished"
				Notify "Operation completed"

			Case "Error"
				DebugLog "An error occurred: " + TRackspaceCloudBaseException(context).ToString()
		End Select

		Self.UpdateProgressBars()
	End Method

	Method UpdateProgressBars()
		If Self.totalFileCount> 0 Then UpdateProgBar(Self.ProgBarTotal, Double(Double(Self.fileCount) / Double(Self.totalFileCount)))

		If Self.currentFileTotalSize > 0 Then UpdateProgBar(Self.ProgBarCurrent, Double(Double(Self.currentFileSize) / Double(Self.currentFileTotalSize)))		

	End Method

	Function progressFunction:Int(data:Object, dltotal:Double, dlnow:Double, ultotal:Double, ulnow:Double)
		Local updater:TProgressBarUpdater = TProgressBarUpdater(data)
		
		If TBackup.backupDirectory.Length > 0
			updater.currentFileSize = ulnow
			updater.currentFileTotalSize = ultotal
		Else If TBackup.restoreDirectory.Length > 0
			updater.currentFileSize = dlnow
			updater.currentFileTotalSize = dltotal
		End If

		updater.UpdateProgressBars()
		'Make sure the OS can redraw the GUI
		'https://github.com/Htbaa/Rackspace-Cloud-Files-Sync/issues/7
		Driver.Poll

		Return 0
	End Function
End Type

'End Of external Append File